@top Result { Expression }

@skip { whitespace | BlockComment | LineComment }

Expression { Operator | Term }

CommaSep<Content> { "" | Content ("," Content)* ","? }

Variable {
    Var
}

Lambda {
    ("(" CommaSep<Variable> ~lambda ")" "=>" Expression) |
    (Variable "=>" Expression)
}

FunctionArg {
    Lambda |
    Expression
}

ObjElem {
    Expression ":" Expression |
    "..." Expression
}

ArrElem {
    Expression | "..." Expression
}

FunctionName {
    Var | FloatTy | StringTy | IntTy
}

FunctionCall {
    FunctionName "(" CommaSep<FunctionArg> ")" |
    Term "." FunctionName "(" CommaSep<FunctionArg> ")"
}

Object { "{" CommaSep<ObjElem> "}" }
Array { "[" CommaSep<ArrElem> "]" }
Selector {
    Term "." Variable |
    Term "[" Expression "]"
}

Term {
    Number | Boolean | String | Null |
    FunctionCall |
    Variable |
    Array |
    Object |
    Selector |
    "(" Expression ~lambda ")"
}

@precedence { t8 @right, t7 @left, t6 @left, t5 @left, t4 @left, t3 @left, t2 @left }

Operator {
    !t8 (LogicOp<"!"> | ArithOp<"-">) Expression |
    Expression !t7 (ArithOp<"*"> | ArithOp<"/"> | ArithOp<"%">) Expression |
    Expression !t6 (ArithOp<"+"> | ArithOp<"-">) Expression |
    Expression !t5 (CompareOp<">"> | CompareOp<"<"> | CompareOp<">="> | CompareOp<"<=">) Expression |
    Expression !t5 CompareOp<"is"> Type |
    Expression !t4 (CompareOp<"=="> | CompareOp<"!=">) Expression |
    Expression !t3 (LogicOp<"&&">) Expression |
    Expression !t2 (LogicOp<"||">) Expression
}

ArithOp<expr> { expr }
LogicOp<expr> { expr }
CompareOp<expr> { expr }

BlockComment { blockComment }
LineComment { lineComment }

@external tokens doubleQuoteStringContent from "./tokens.js" { doubleQuoteStringContent }
@external tokens singleQuoteStringContent from "./tokens.js" { singleQuoteStringContent }
@external tokens identifierContent from "./tokens.js" { identifierContent }

Boolean { @specialize<PlainVar, "true"> | @specialize<PlainVar, "false"> }

Type {
    Null | IntTy | BoolTy | FloatTy | StringTy | ArrayTy | ObjectTy | NumberTy
}
Null { @specialize<PlainVar, "null"> }
IntTy { @specialize<PlainVar, "int"> }
BoolTy { @specialize<PlainVar, "bool"> }
FloatTy { @specialize<PlainVar, "float"> }
StringTy { @specialize<PlainVar, "string"> }
ArrayTy { @specialize<PlainVar, "array"> }
ObjectTy { @specialize<PlainVar, "object"> }
NumberTy { @specialize<PlainVar, "number"> }


@external tokens blockComment from "./tokens.js" { blockComment }
@external tokens lineComment from "./tokens.js" { lineComment }

@skip {} {
  String {
    "\"" doubleQuoteStringContent "\"" |
    "'" singleQuoteStringContent "'"
  }
  Var {
    PlainVar |
    "`" identifierContent "`"
  }
}

@tokens {
    Number { $[0-9]* "." $[0-9]+ | $[0-9]* "."? $[0-9]+ $[eE] $[+-]? $[0-9]+ | $[0-9]+ | $[0-9]+ }
    whitespace { @whitespace+ }
    PlainVar { $[a-zA-Z_]$[a-zA-Z0-9_]* }
    "(" ")" "{" "}" "[" "]" "!" "*" "/" "%" "+" "-" ">" "<" "<=" ">=" "is" "==" "!=" "&&" "||"
    "," "." ":" "..."
    "=>"[@name=Arrow]
}
